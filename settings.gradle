pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            plugin 'javadoc-links', 'io.freefair.javadoc-links' version '8.13.1'

            // https://mvnrepository.com/artifact/org.jspecify/jspecify
            library('jspecify-annotations', 'org.jspecify', 'jspecify') version '1.0.0'

            // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
            library('errorprone-core', 'com.google.errorprone', 'error_prone_core') version '2.36.0'

            // https://mvnrepository.com/artifact/com.uber.nullaway/nullaway
            library('nullaway', 'com.uber.nullaway', 'nullaway') version '0.12.4'

            // ModLauncher stack
            library('modlauncher', 'net.minecraftforge:modlauncher:10.1.1')
            library('securemodules', 'net.minecraftforge:securemodules:2.2.21')
            library('unsafe', 'net.minecraftforge:unsafe:0.9.2')

            // Generics from lambdas
            library('typetools', 'net.jodah:typetools:0.6.3')

            library('nulls', 'org.jetbrains:annotations:23.0.0')
            library('jopt-simple', 'net.sf.jopt-simple:jopt-simple:5.0.4')
            library('log4j-api', 'org.apache.logging.log4j:log4j-api:2.17.1')

            version('junit', '5.10.1')
            library('junit-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            library('junit-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
            library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.10.1')
            bundle('junit-runtime', ['junit-engine', 'junit-platform-launcher'])

            version('asm', '9.7.1')
            library('asm',         'org.ow2.asm', 'asm'        ).versionRef('asm')
            library('asm-tree',    'org.ow2.asm', 'asm-tree'   ).versionRef('asm')
            library('asm-commons', 'org.ow2.asm', 'asm-commons').versionRef('asm')
            bundle('asm', ['asm', 'asm-tree', 'asm-commons'])

            version('jmh', '1.37')
            library('jmh-core', 'org.openjdk.jmh', 'jmh-core') versionRef 'jmh'
            library('jmh-annotationProcessor', 'org.openjdk.jmh', 'jmh-generator-annprocess') versionRef 'jmh'
        }
    }
}

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

rootProject.name = 'EventBus'
include 'eventbus-jmh'
include 'eventbus-test'
include 'eventbus-test-jar'
if (file('eventbus-wrapper').exists())
    include 'eventbus-wrapper'
