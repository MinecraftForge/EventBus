plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

dependencyResolutionManagement {
    repositories {
        // TODO [Gradle9][GradleUtils3] Replace with gradleutils.forgeMaven
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }

    //@formatter:off
    versionCatalogs.register('libs') {
        plugin 'modules',     'org.gradlex.extra-java-module-info' version '1.13.1'
        plugin 'gradleutils', 'net.minecraftforge.gradleutils'     version '3.3.39'
        plugin 'gitversion',  'net.minecraftforge.gitversion'      version '3.1.7'
        plugin 'licenser',    'net.minecraftforge.licenser'        version '1.2.0'
        plugin 'changelog',   'net.minecraftforge.changelog'       version '3.2.1'

        plugin 'errorprone', 'net.ltgt.errorprone' version '4.3.0'
        plugin 'nullaway',   'net.ltgt.nullaway'   version '2.3.0'

        // https://mvnrepository.com/artifact/org.jspecify/jspecify
        library 'jspecify-annotations', 'org.jspecify', 'jspecify' version '1.0.0'

        // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
        library 'errorprone-core', 'com.google.errorprone', 'error_prone_core' version '2.45.0'

        // https://mvnrepository.com/artifact/com.uber.nullaway/nullaway
        library 'nullaway', 'com.uber.nullaway', 'nullaway' version '0.12.14'


        /* The rest is for testing */
        version 'junit', '6.0.1'
        library 'junit-api',               'org.junit.jupiter',  'junit-jupiter-api'       versionRef 'junit'
        library 'junit-engine',            'org.junit.jupiter',  'junit-jupiter-engine'    versionRef 'junit'
        library 'junit-platform-launcher', 'org.junit.platform', 'junit-platform-launcher' versionRef 'junit'
        bundle 'junit-runtime', ['junit-engine', 'junit-platform-launcher']

        version 'asm', '9.9'
        library 'asm',         'org.ow2.asm', 'asm'         versionRef 'asm'
        library 'asm-tree',    'org.ow2.asm', 'asm-tree'    versionRef 'asm'
        library 'asm-commons', 'org.ow2.asm', 'asm-commons' versionRef 'asm'
        bundle 'asm', ['asm', 'asm-tree', 'asm-commons']

        version 'jmh', '1.37'
        library 'jmh-core',                'org.openjdk.jmh', 'jmh-core'                 versionRef 'jmh'
        library 'jmh-annotationProcessor', 'org.openjdk.jmh', 'jmh-generator-annprocess' versionRef 'jmh'

        library 'compile-testing', 'com.google.testing.compile', 'compile-testing' version '0.23.0'
        library 'jetbrains-annotations', 'org.jetbrains', 'annotations' version '26.0.2'
    }
    //@formatter:on
}

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

rootProject.name = 'EventBus'
include 'eventbus-jmh'
include 'eventbus-test'
include 'eventbus-test-jar'
include 'eventbus-validator'
if (file('eventbus-wrapper').exists())
    include 'eventbus-wrapper'
