import net.ltgt.gradle.errorprone.ErrorProneOptions
import net.ltgt.gradle.nullaway.NullAwayOptions

plugins {
    id 'java-library'
    id 'maven-publish'
    alias libs.plugins.modules
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.licenser

    // Enforce jSpecify annotations at compile-time
    alias libs.plugins.errorprone
    alias libs.plugins.nullaway
}

final projectDisplayName = 'EventBus'
final projectVendor = 'Forge Development LLC'
description = 'High performance Event Bus library'
group = 'net.minecraftforge'
version = gitversion.tagOffset

print "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    modularity.inferModulePath = true
    withSourcesJar()
}

dependencies {
    api libs.jspecify.annotations
    errorprone libs.errorprone.core
    errorprone libs.nullaway
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
}

changelog {
    from '1.0.0'
}

tasks.withType(JavaCompile).configureEach {
    // Set up compile-time enforcement of the jSpecify spec via ErrorProne and NullAway
    options.errorprone { ErrorProneOptions errorProne ->
        errorProne.disableAllChecks = true // Opt-into only the checks we care about

        // Enforce the jSpecify spec
        errorProne.nullaway { NullAwayOptions nullAway ->
            nullAway.jspecifyMode = true
            nullaway.onlyNullMarked = true // Only enforce nullability checks on @NullMarked classes and packages
            nullAway.error() // Throw a compile error on jSpecify spec violations
        }

        // Enforce the ErrorProne checks we care about
        errorProne.error 'FieldCanBeFinal', 'MethodCanBeStatic', 'LambdaFunctionalInterface'
    }

    // Needed for Java 21, not needed on Java 22+. This flag requires javac 21.0.8+ to work.
    // See https://github.com/uber/NullAway/wiki/JSpecify-Support#supported-jdk-versions
    options.compilerArgs += '-XDaddTypeAnnotationsToSymbol=true'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Version' : gitversion.info.tag,
            'Specification-Vendor'  : projectVendor,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Version': project.version,
            'Implementation-Vendor' : projectVendor
        ])
    }
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'eventbus'
        pom {
            name = projectDisplayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Paint_Ninja
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

allprojects {
    // Tests are expensive to run all variants, so only run if asked to
    if (!project.hasProperty('bulk_tests'))
        ext.VALID_VMS = ['Adoptium':  [21]]
    else
        ext.VALID_VMS = [
            'Adoptium':  [21],
            'Amazon':    [21],
            'Azul':      [21],
            'BellSoft':  [21],
            'Graal_VM':  [21],
            'Microsoft': [21],
            'Oracle':    [21],
        ]
}
