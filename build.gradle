import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.50.0'
    id 'org.gradlex.extra-java-module-info' version '1.4.2'
    id 'net.minecraftforge.gradleutils' version '2.3.3'
    id 'net.minecraftforge.licenser' version '1.0.1'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
print("Version: $version")

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    modularity.inferModulePath.set(true)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    api(libs.typetools)
    implementation(libs.log4j.api)
    implementation(libs.bundles.asm)
    compileOnly(libs.modlauncher)
    compileOnly(libs.securemodules)
    compileOnly(libs.nulls)
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
}

changelog {
    from '1.0.0'
}

jar {
    manifest {
        attributes([
            'Specification-Title':   'EventBus',
            'Specification-Version':  gradleutils.gitInfo.tag,
            'Specification-Vendor':   'Forge Development LLC',
            'Implementation-Title':   'EventBus',
            'Implementation-Version': project.version,
            'Implementation-Vendor':  'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/eventbus/service/')
    }
}

license {
    header = file("LICENSE-header.txt")
    newLine = false
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'eventbus'
        pom {
            name = 'Event Bus'
            description = 'High performance Event Bus library'
            url = 'https://github.com/MinecraftForge/EventBus'

            PomUtils.setGitHubDetails(pom, 'EventBus')

            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
                developer PomUtils.Developers.cpw
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

allprojects {
    ext.VALID_VMS = [
        'Adoptium':  [16, 17, 18, 19, 20, 21],
        'Amazon':    [16, 17, 18, 19, 20, 21],
        'Azul':      (16..21),
        'BellSoft':  (16..21),
        'Graal_VM':  [16, 17,     19, 20, 21],
        'IBM':       [16, 17, 18, 19, 20    ],
        'Microsoft': [16, 17,             21],
        'Oracle':    (16..21),
        'SAP':       (16..20)
    ]

    // Tests are expensive to run all variants, so only run if asked to
    if (!project.hasProperty('bulk_tests'))
        ext.VALID_VMS = ['Adoptium':  [17]]//, 18, 19, 20, 21] ]
}