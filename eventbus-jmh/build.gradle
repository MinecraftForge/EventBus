plugins {
    id 'eclipse'
    id 'java-library'
    id 'org.gradlex.extra-java-module-info' version '1.4.2'
    id 'net.minecraftforge.gradleutils' version '[2.1.2,)'
    id 'com.diffplug.eclipse.apt' version '3.43.0'
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
}

dependencies {
    compileOnly(libs.nulls)
    
    implementation(rootProject)
    implementation(libs.bundles.asm) // Needed by ModLauncher
    implementation(libs.junit.api)
    implementation(libs.securemodules)
    implementation(libs.modlauncher)
    implementation(project(':eventbus-test-jar'))
    
    implementation('org.openjdk.jmh:jmh-core:1.35')
    runtimeOnly('org.openjdk.jmh:jmh-generator-annprocess:1.35')
    annotationProcessor('org.openjdk.jmh:jmh-generator-annprocess:1.35')
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
    automaticModule('jmh-core-1.35.jar', 'jmh.core')
    automaticModule('jopt-simple-5.0.4.jar', 'jopt.simple')
}

def jmhArgs = [
    '-bm', 'AverageTime',  // Benchmark mode. Available modes are: [Throughput, AverageTime, SampleTime, SingleShotTime, All]
    '-r',  '5s',  // Minimum iteration time
    '-w',  '5s',  // Minimum warmup time
    '-wi', '2',   // Warmup iterations
    '-i',  '2',   // Iterations
    '-tu', 'ns',  // Time unit: [m, s, ms, us, ns]
    '-f',  '1',   // Forks per benchmark
    '-rf', 'json', // Results File Format
    '-prof', 'stack', // Profiler: Simple and naive Java stack profiler
    '-prof', 'jfr',   // Profiler: Java Flight Recorder profiler
]

tasks.register('jmh', JavaExec) {
    dependsOn(rootProject.build)
    dependsOn(build)
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.openjdk.jmh.Main'
    args jmhArgs + ['-rff', rootProject.file('build/jmh_results.json')]

    if (project.hasProperty('javaVendor') && project.hasProperty('javaVersion')) {
        javaLauncher.set(javaToolchains.launcherFor {
            it.vendor.set(JvmVendorSpec."${project.property('javaVendor').toUpperCase(Locale.ROOT)}" as JvmVendorSpec)
            it.languageVersion.set(JavaLanguageVersion.of(project.property('javaVersion') as int))
            it.implementation.set(JvmImplementation.VENDOR_SPECIFIC)
        })
    } else {
        javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
    }
}

// JMH's are expensive to run all variants, so only run if asked to
if (project.hasProperty('local_jmh')) { 
    configurations {
        groovyScript
    }

    dependencies {
        groovyScript 'org.apache.ivy:ivy:2.4.0'
        groovyScript 'org.codehaus.groovy:groovy-all:3.0.19'
    }

    tasks.register('jmhAll', JavaExec) {
        classpath = configurations.groovyScript
        main = 'groovy.ui.GroovyMain'
        args '.github/workflows/aggregate-jmh-results.groovy'
        workingDir rootProject.projectDir
    }

    VALID_VMS.each { javaVendor, javaVersions ->
        javaVersions.each { javaVersion ->
            def task = tasks.register("jmh${javaVendor}${javaVersion}", JavaExec) {
                def output = rootProject.file("build/jmh_results/jmh-${javaVendor}${javaVersion}/jmh_results.json")
                dependsOn(rootProject.build)
                dependsOn(build)
                classpath = sourceSets.main.runtimeClasspath
                mainClass = 'org.openjdk.jmh.Main'
                args jmhArgs + [
                    '-v', 'SILENT', // Verbosity [SILENT, NORMAL, EXTRA]
                    '-rff', output
                ]
                doFirst { output.parentFile.mkdirs() }
                javaLauncher.set(javaToolchains.launcherFor {
                    it.vendor.set(JvmVendorSpec."${javaVendor.toUpperCase(Locale.ROOT)}" as JvmVendorSpec)
                    it.languageVersion.set(JavaLanguageVersion.of(javaVersion))
                    it.implementation.set(JvmImplementation.VENDOR_SPECIFIC)
                })
            }
            jmhAll.dependsOn(task)
        }
    }
}

// Hack eclipse to add the annotation processors in TODO: Upstream this to com.diffplug.eclipse.apt ?
eclipse {
    synchronizationTasks(eclipseFactorypath, eclipseJdt, eclipseJdtApt)
    classpath {
        file.beforeMerged {
            file('.apt_generated').mkdirs()
            def generated = new org.gradle.plugins.ide.eclipse.model.SourceFolder('.apt_generated', 'bin/.apt_generated')
            generated.entryAttributes['ignore_optional_problems'] = true
            entries << generated
        }
    }
}

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}
