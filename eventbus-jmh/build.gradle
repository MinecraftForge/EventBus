configurations {
    jmhOnly
}

dependencies {
    implementation(rootProject)
    implementation(project(':eventbus-testjars'))
    implementation('cpw.mods:modlauncher:10.0.+')
    implementation('cpw.mods:securejarhandler:2.0.+')
    implementation('org.junit.jupiter:junit-jupiter-engine:5.8.+')
    implementation('org.apache.logging.log4j:log4j-core:2.17.1')
    implementation('org.apache.logging.log4j:log4j-api:2.17.1')
    implementation('org.ow2.asm:asm:9.2')
    implementation('org.ow2.asm:asm-tree:9.2')
    implementation('org.ow2.asm:asm-commons:9.2')
    implementation('org.openjdk.jmh:jmh-core:1.35')
    implementation('cpw.mods:bootstraplauncher:1.1.0')
    jmhOnly('org.openjdk.jmh:jmh-core:1.35')
    jmhOnly('org.openjdk.jmh:jmh-generator-annprocess:1.35')
    jmhOnly('cpw.mods:bootstraplauncher:1.1.0')
    jmhOnly('cpw.mods:securejarhandler:2.0.+')
    jmhOnly(sourceSets.main.output)
    compileOnly('org.jetbrains:annotations:23.0.0')
    runtimeOnly('org.apiguardian:apiguardian-api:1.1.2')
    annotationProcessor('org.openjdk.jmh:jmh-generator-annprocess:1.35')
}

task jmh(type: JavaExec, dependsOn: sourceSets.main.output) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    jvmArgs= [
        '-p', sourceSets.main.runtimeClasspath.asPath,
        '--add-modules', 'ALL-MODULE-PATH',
        '--add-exports', 'net.minecraftforge.eventbus.jmh/net.minecraftforge.eventbus.benchmarks.jmh_generated=jmh.core',
        '--add-opens', 'java.base/java.lang.invoke=cpw.mods.securejarhandler'
    ]
    classpath = files(configurations.jmhOnly.asPath)
    mainClass = 'org.openjdk.jmh.Main'
    args    '-bm', 'avgt'  // benchmark mode
    args    '-r', '5s' // iteration time
    args    '-w', '5s' // warmup time
    args    '-wi', '3' // warmup iterations
    args    '-prof', 'stack' // profilers
    args    '-tu', 'ns' // time unit
    args    '-f', '3' // forks
    args    '-rff', project.file("${rootProject.buildDir}/jmh_results.txt")  // results file
    args    'net.minecraftforge.eventbus.benchmarks.EventBusBenchmark'
    args    'net.minecraftforge.eventbus.benchmarks.EventBusBenchmarkNoLoader'
}