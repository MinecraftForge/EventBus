plugins {
    id 'eclipse'
    id 'java-library'
    id 'org.gradlex.extra-java-module-info'
    id 'net.minecraftforge.gradleutils'
    id 'net.minecraftforge.licenser'
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

dependencies {
    implementation(rootProject)
    implementation(libs.bundles.asm)
    implementation(libs.unsafe)

    implementation libs.jmh.core
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
    automaticModule('org.openjdk.jmh:jmh-core', 'jmh.core')
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

tasks.named('javadoc', Javadoc) {
    enabled = false
}

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}
